(* brainfuckインタプリタ
   入力の形式:
     {brainfuckのコード}\{入力}
*)

// 不動点コンビネータ
let fix f =
  let g x = f (fun y -> x x y) in
  g g

// リスト l を比較関数 compare でソートする
let sort compare l =
  fix
    (fun sort -> fun rest sorted ->
      match rest with
      | () -> sorted
      | pivot . rest ->
          fix
            (fun partition -> fun left right l ->
              match l with
              | () -> sort left (pivot . sort right sorted)
              | hd . tl ->
                  if compare hd pivot < 0
                  then partition (hd . left) right tl
                  else partition left (hd . right) tl
            ) () () rest
    ) l ()

// セル x の中身をゼロにする
let gen_del x = [
  ! x [ - x ]
]

// セル dest からセル src の中身だけ引く
// src はゼロになる
// オーバーフローするとき文列 overflow を実行する
let gen_sub_safe src dest overflow = [
  ! src [
    - src
    ? dest [ - dest ] [
      *overflow
      *gen_del src
    ]
  ]
]

(* セル sel の中身で分岐する
   sel は破壊される
   整数と文列のペアのリスト cases の各要素のペアの整数を小さい順に見て
   sel の中身と同じであれば文列を実行する
   該当するものがなければ文列 default を実行する
   文列中で sel の中身が変化しても良い
*)
let gen_switch sel cases default = [
  **let cases =
    sort (fun (n1, _) (n2, _) -> n1 - n2) cases
  in $
  $alloc {
    sub_src; flag_eq; flag_els
  } in
  *fix
    (fun loop prev cases ->
      match cases with
      | () -> default
      | (num, block) . cases -> [
          + sub_src (num - prev)
          + flag_eq
          *gen_sub_safe sub_src sel [ - flag_eq ]
          ? sel [ - flag_eq ] []
          + flag_els
          ! flag_eq [
            - flag_eq
            - flag_els
            *block
          ]
          ! flag_els [
            - flag_els
            *loop num cases
          ]
        ])
    0 cases
]


// セル dest にセル src の中身を足す
// srcはゼロになる
let gen_add src dest = [
  ! src [ - src + dest ]
]


// セルと係数のペアのリスト dest_and_coef_list の各要素のセルに対して
// セル src の中身に係数をかけた数を足す
// セル src の中身はゼロになる
let gen_add_multi src dest_and_coef_list = [
  ! src [
    - src
    *fix
      (fun loop l ->
        match l with
        | () -> []
        | (dest, coef) . l -> [
            + dest coef
            *loop l
          ])
      dest_and_coef_list
  ]
]

// gen_add_multi と同様
// ただしセル src は復元される
let gen_add_multi_restore src dest_and_coef_list = [
  $alloc { backup } in
  *gen_add_multi src ((backup, 1) . dest_and_coef_list)
  *gen_add backup src
]

// セル dest にセル src の中身を足す
// src は復元される
let gen_add_restore src dest = [
  *gen_add_multi_restore src ((dest, 1);)
]

// セル x の中身を数値 n にする
let gen_set x n = [
  *gen_del x
  + x n
]

(* セル sel の中身で分岐する
   sel は分岐の前に復元される
   sel の中身を i とする。文列のリスト blocks の
   第 i 要素が存在する場合それを実行する
   第 i 要素が存在しない場合文列 default を実行する
   文列中で sel の中身が変化しても良い
*)
let gen_switch_nat sel blocks default = [
  *fix
    (fun loop i blocks ->
      match blocks with
      | () -> [
          + sel i
          *default
        ]
      | block . rest -> [
          ? sel [
            - sel
            *loop (i + 1) rest
          ] [
            + sel i
            *block
          ]
        ])
    0 blocks
]


codegen $

// コード入力
$build {
  code: array(_) {
    inst: cell;
    depth: cell;
    p: index;
  }
} in

$dive code@p [
  **let p = code@p in $
  $alloc {
    cont; flag_next
  } in
  + cont
  ! cont [
    , p:inst
    + flag_next
    *gen_switch p:inst
      ( ('+', [ *gen_set p:inst 1 ]);
        ('-', [ *gen_set p:inst 2 ]);
        ('>', [ *gen_set p:inst 3 ]);
        ('<', [ *gen_set p:inst 4 ]);
        ('[', [
          *gen_set p:inst 5
          + p:(1)depth
        ]);
        (']', [
          *gen_set p:inst 6
          - p:depth
        ]);
        ('.', [ *gen_set p:inst 7 ]);
        (',', [ *gen_set p:inst 8 ]);
        ('\\', [
          - flag_next
          - cont
        ]);
      )
      [ - flag_next ]
    ! flag_next [
      - flag_next
      *gen_add_restore p:depth p:(1)depth
      > p
    ]
  ]
]

$iloop code@p [ < code@p ]


// 実行
$build {
  tape: array(_) {
    v: cell;
    p: index;
  }
} in

**let p = code@p in $
$dive p [
  $alloc { depth_cur } in
  ! p:inst [
    *gen_switch_nat p:inst
      ( [];
        [ + tape@p:v ];
        [ - tape@p:v ];
        [ > tape@p ];
        [ < tape@p ];
        [ // while
          ? tape@p:v [] [
            *gen_add_multi_restore p:depth
              ( (depth_cur, 1);
                (p:(1)depth, -1) )
            > p
            ! p:depth [
              *gen_add_multi_restore depth_cur
                ( (p:depth, 1);
                  (p:(1)depth, -1) )
              > p
            ]
            *gen_add depth_cur p:depth
          ]
        ];
        [ // wend
          *gen_add_multi_restore p:depth
            ( (depth_cur, 1);
              (p:(-1)depth, -1) )
          < p
          ! p:depth [
            *gen_add_multi_restore depth_cur
              ( (p:depth, 1);
                (p:(-1)depth, -1) )
            < p
          ]
          *gen_add depth_cur p:depth
          < p
        ];
        [ . tape@p:v ];
      )
      [ , tape@p:v ]
    > p
  ]
]